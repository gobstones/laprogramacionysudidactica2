procedure TestsDeCartasEspañolasDe40(códigoDeTest) {
  if (códigoDeTest == 1) {
    TestCartaYPonerCarta()
  } elseif (códigoDeTest == 2) {
    TestSacarCarta()
  } elseif (códigoDeTest == 3) {
    TestDarVueltaLaCartaActual1()
  } elseif (códigoDeTest == 4) {
    TestDarVueltaLaCartaActual2()
  } elseif (códigoDeTest == 5) {
    TestHayCarta()
    TestHayCartaTapada()
  } elseif (códigoDeTest == 6) {
    TestCartaActual()
  } elseif (códigoDeTest == 7) {
    TestCartaTapada()
  } elseif (códigoDeTest == 8) {
    TestEsCartaMenorQue()
  } elseif (códigoDeTest == 9) {
    TestEsMayorCartaQue()
  } elseif (códigoDeTest == 10) {
    TestSiguienteYPrevio()
  } elseif (códigoDeTest == 11) {
    TestMinCartaYMaxCarta()
  } elseif (códigoDeTest == 12) {
    TestIluminarLaCartaActual()
  } elseif (códigoDeTest == 13) {
    TestHayCartaIluminada()
  } else {
    BOOM("Pasó todos los tests")
  }
}

procedure TestCartaYPonerCarta() {
  PonerTodasLasCartas()
  Assert(0 == 0)
}

procedure PonerTodasLasCartas() {
  paloActual := minPalo()
  while (not (paloActual == maxPalo())) {
    PonerTodasLasCartasDePalo(paloActual)
    paloActual := paloSiguiente_(paloActual)
    PasarASiguientePosición()
  }
  PonerTodasLasCartasDePalo(paloActual)
}

procedure PonerTodasLasCartasDePalo(paloAPoner) {
  valorActual := minValor()
  while (not (valorActual == maxValor())) {
    PonerCarta_(dameCarta__(valorActual, paloAPoner))
    valorActual := valorSiguiente_(valorActual)
    PasarASiguientePosición()
  }
  PonerCarta_(dameCarta__(valorActual, paloAPoner))
}

procedure TestSacarCarta() {
  PonerTodasLasCartas()
  IrAlOrigen()
  repeat(40) {
    PasarASiguientePosición()
    SacarCartaActual()
  }
  Assert(0 == 0)
}

procedure TestDarVueltaLaCartaActual1() {
  PonerTodasLasCartasYDarlasVuelta()
  Assert(0 == 0)
}

procedure TestDarVueltaLaCartaActual2() {
  PonerTodasLasCartasYDarlasVuelta()
  IrAlOrigen()
  repeat(40) {
    PasarASiguientePosición()
    DarVueltaLaCartaActual()
  }
  Assert(0 == 0)
}

procedure PonerTodasLasCartasYDarlasVuelta() {
  PonerTodasLasCartas()
  IrAlOrigen()
  repeat(40) {
    PasarASiguientePosición()
    DarVueltaLaCartaActual()
  }
}

procedure TestCartaActual() {
  PonerCarta_(dameCarta__(valorDeCartaEspañolaDe40_(2), palo_(Copas)))
  Assert(palo_(Copas) == dameElPalo_(cartaActual()) && valorDeCartaEspañolaDe40_(2) == dameElValor_(cartaActual()))
}

procedure TestHayCarta() {
  PonerCarta_(dameCarta__(valorDeCartaEspañolaDe40_(1), palo_(Bastos)))
  Assert(hayCarta())
  PasarASiguientePosición()
  SacarTodas()
  Assert(not(hayCarta()))
}

procedure TestHayCartaTapada() {
  PasarASiguientePosición()
  PonerCarta_(dameCarta__(valorDeCartaEspañolaDe40_(1), palo_(Oros)))
  DarVueltaLaCartaActual()
  Assert(hayCartaTapada())
  PasarASiguientePosición()
  PonerCarta_(dameCarta__(valorDeCartaEspañolaDe40_(1), palo_(Oros)))
  Assert(not(hayCartaTapada()))
  PasarASiguientePosición()
  SacarTodas()
  Assert(not(hayCartaTapada()))
}

procedure Assert(condiciónAVerificar) {
  PasarASiguientePosición()
  if (condiciónAVerificar) {
    Poner__Veces(Verde, 1)
  } else {
    Poner__Veces(Rojo, 1)
  }
}

procedure TestCartaTapada() {
  PonerCarta_(dameCarta__(valorDeCartaEspañolaDe40_(7), palo_(Oros)))
  DarVueltaLaCartaActual()
  Assert(palo_(Oros) == dameElPalo_(cartaTapada()) && valorDeCartaEspañolaDe40_(7) == dameElValor_(cartaTapada()))
}

procedure LimpiarTablero() {
  PasarASiguientePosición()
  repeat(47) {
    SacarTodas()
    PasarASiguientePosición()
  }
  PasarASiguientePosición()
}

procedure TestEsCartaMenorQue() {
  TestearQueUnaCartaEsMenorQueOtra(dameCarta__(valorDeCartaEspañolaDe40_(3), palo_(Espadas)), dameCarta__(valorDeCartaEspañolaDe40_(5), palo_(Espadas)))
  PasarASiguientePosición()
  TestearQueUnaCartaEsMenorQueOtra(dameCarta__(valorDeCartaEspañolaDe40_(6), palo_(Oros)), dameCarta__(valorDeCartaEspañolaDe40_(11), palo_(Oros)))
  PasarASiguientePosición()
  TestearQueUnaCartaEsMenorQueOtra(dameCarta__(valorDeCartaEspañolaDe40_(11), palo_(Espadas)), dameCarta__(valorDeCartaEspañolaDe40_(12), palo_(Espadas)))
  PasarASiguientePosición()
  TestearQueUnaCartaEsMenorQueOtra(dameCarta__(valorDeCartaEspañolaDe40_(12), palo_(Espadas)), dameCarta__(valorDeCartaEspañolaDe40_(1), palo_(Oros)))
  PasarASiguientePosición()
  TestearQueUnaCartaEsMenorQueOtra(dameCarta__(valorDeCartaEspañolaDe40_(12), palo_(Espadas)), dameCarta__(valorDeCartaEspañolaDe40_(11), palo_(Oros)))
  PasarASiguientePosición()
  TestearQueUnaCartaEsMenorQueOtra(dameCarta__(valorDeCartaEspañolaDe40_(11), palo_(Copas)), dameCarta__(valorDeCartaEspañolaDe40_(3), palo_(Oros)))
  PasarASiguientePosición()
  TestearQueUnaCartaEsMenorQueOtra(dameCarta__(valorDeCartaEspañolaDe40_(7), palo_(Bastos)), dameCarta__(valorDeCartaEspañolaDe40_(2), palo_(Espadas)))
}

procedure TestearQueUnaCartaEsMenorQueOtra(carta1, carta2) {
  PonerCarta_(carta1)
  PasarASiguientePosición()
  PonerCarta_(carta2)
  Assert(esMenorCarta__(carta1, carta2))
  Assert(not(esMenorCarta__(carta2, carta1)))
  Assert(not(esMenorCarta__(carta1, carta1)))
}

procedure TestEsMayorCartaQue() {
  TestearQueUnaCartaEsMayorQueOtra(dameCarta__(valorDeCartaEspañolaDe40_(4), palo_(Espadas)), dameCarta__(valorDeCartaEspañolaDe40_(6), palo_(Espadas)))
  PasarASiguientePosición()
  TestearQueUnaCartaEsMayorQueOtra(dameCarta__(valorDeCartaEspañolaDe40_(7), palo_(Oros)), dameCarta__(valorDeCartaEspañolaDe40_(12), palo_(Oros)))
  PasarASiguientePosición()
  TestearQueUnaCartaEsMayorQueOtra(dameCarta__(valorDeCartaEspañolaDe40_(10), palo_(Espadas)), dameCarta__(valorDeCartaEspañolaDe40_(11), palo_(Espadas)))
  PasarASiguientePosición()
  TestearQueUnaCartaEsMayorQueOtra(dameCarta__(valorDeCartaEspañolaDe40_(11), palo_(Bastos)), dameCarta__(valorDeCartaEspañolaDe40_(1), palo_(Espadas)))
  PasarASiguientePosición()
  TestearQueUnaCartaEsMayorQueOtra(dameCarta__(valorDeCartaEspañolaDe40_(11), palo_(Espadas)), dameCarta__(valorDeCartaEspañolaDe40_(10), palo_(Oros)))
  PasarASiguientePosición()
  TestearQueUnaCartaEsMayorQueOtra(dameCarta__(valorDeCartaEspañolaDe40_(12), palo_(Copas)), dameCarta__(valorDeCartaEspañolaDe40_(4), palo_(Oros)))
  PasarASiguientePosición()
  TestearQueUnaCartaEsMayorQueOtra(dameCarta__(valorDeCartaEspañolaDe40_(6), palo_(Bastos)), dameCarta__(valorDeCartaEspañolaDe40_(3), palo_(Espadas)))
}

procedure TestearQueUnaCartaEsMayorQueOtra(carta1, carta2) {
  PonerCarta_(carta1)
  PasarASiguientePosición()
  PonerCarta_(carta2)
  Assert(esMayorCarta__(carta2, carta1))
  Assert(not(esMayorCarta__(carta1, carta2)))
  Assert(not(esMayorCarta__(carta1, carta1)))
}

procedure TestSiguienteYPrevio() {
  TestSiguienteYPrevioEnMedio()
  TestSiguienteYPrevioEnBordeInferior()
  TestSiguienteYPrevioEnBordeSuperior()
}

procedure TestSiguienteYPrevioEnMedio() {
  TesteaSiguienteYPrevio(dameCarta__(valorDeCartaEspañolaDe40_(3), palo_(Espadas)), dameCarta__(valorDeCartaEspañolaDe40_(4), palo_(Espadas)), dameCarta__(valorDeCartaEspañolaDe40_(2), palo_(Espadas)))
}

procedure TestSiguienteYPrevioEnBordeInferior() {
  PasarASiguientePosición()
  TesteaSiguienteYPrevio(dameCarta__(valorDeCartaEspañolaDe40_(1), palo_(Bastos)), dameCarta__(valorDeCartaEspañolaDe40_(2), palo_(Bastos)), dameCarta__(valorDeCartaEspañolaDe40_(12), palo_(Oros)))
  PasarASiguientePosición()
  TesteaSiguienteYPrevio(dameCarta__(armarValorAs(), espadas()), dameCarta__(armarValor_(2), espadas()), dameCarta__(armarValorRey(), copas()))
  PasarASiguientePosición()
  TesteaSiguienteYPrevio(dameCarta__(armarValor_(7), oros()), dameCarta__(armarValorSota(), oros()), dameCarta__(armarValor_(6), oros()))
}

procedure TestSiguienteYPrevioEnBordeSuperior() {
  PasarASiguientePosición()
  TesteaSiguienteYPrevio(dameCarta__(valorDeCartaEspañolaDe40_(12), palo_(Oros)), dameCarta__(valorDeCartaEspañolaDe40_(1), palo_(Bastos)), dameCarta__(valorDeCartaEspañolaDe40_(11), palo_(Oros)))
  PasarASiguientePosición()
  TesteaSiguienteYPrevio(dameCarta__(valorDeCartaEspañolaDe40_(12), palo_(Copas)), dameCarta__(valorDeCartaEspañolaDe40_(1), palo_(Espadas)), dameCarta__(valorDeCartaEspañolaDe40_(11), palo_(Copas)))
  PasarASiguientePosición()
  TesteaSiguienteYPrevio(dameCarta__(valorDeCartaEspañolaDe40_(10), palo_(Copas)), dameCarta__(valorDeCartaEspañolaDe40_(11), palo_(Copas)), dameCarta__(valorDeCartaEspañolaDe40_(7), palo_(Copas)))
}

procedure TesteaSiguienteYPrevio(carta, cartaSiguiente, cartaPrevia) {
  PonerCarta_(cartaPrevia_(carta))
  PasarASiguientePosición()
  PonerCarta_(carta)
  PasarASiguientePosición()
  PonerCarta_(cartaSiguiente_(carta))
  Assert(cartaSiguiente_(carta) == cartaSiguiente)
  Assert(cartaPrevia_(carta) == cartaPrevia)
}

procedure TestMinCartaYMaxCarta() {
  PonerCarta_(minCarta())
  Assert(minCarta() == dameCarta__(valorDeCartaEspañolaDe40_(1), palo_(Bastos)))
  PasarASiguientePosición()
  PonerCarta_(maxCarta())
  Assert(maxCarta() == dameCarta__(valorDeCartaEspañolaDe40_(12), palo_(Oros)))
}

procedure TestIluminarLaCartaActual() {
  PonerTodasLasCartasELiuminarlas()
  Assert(0 == 0)
}

procedure PonerTodasLasCartasELiuminarlas() {
  PonerTodasLasCartas()
  IrAlOrigen()
  repeat(40) {
    PasarASiguientePosición()
    IluminarLaCartaActual()
  }
}

procedure TestHayCartaIluminada() {
  PonerCarta_(dameCarta__(valorDeCartaEspañolaDe40_(1), palo_(Espadas)))
  IluminarLaCartaActual()
  hayOtra := hayOtraCartaDestapadaSinIluminar()
  Assert(not(hayCartaDestapadaSinIluminar()))
  PasarASiguientePosición()
  Assert(not(hayOtra))
  PasarASiguientePosición()
  PonerCarta_(dameCarta__(valorDeCartaEspañolaDe40_(1), palo_(Espadas)))
  Assert(hayCartaDestapadaSinIluminar())
  PasarASiguientePosición()
  SacarTodas()
  Assert(not(hayCartaDestapadaSinIluminar()))
  PasarASiguientePosición()
  PonerCarta_(dameCarta__(valorDeCartaEspañolaDe40_(1), palo_(Espadas)))
  DarVueltaLaCartaActual()
  Assert(not(hayCartaDestapadaSinIluminar()))
  PasarASiguientePosición()
  PonerCarta_(dameCarta__(valorDeCartaEspañolaDe40_(1), palo_(Bastos)))
  IluminarLaCartaActual()
  hayOtra := hayOtraCartaDestapadaSinIluminar()
  PasarASiguientePosición()
  Assert(hayOtra)
}


interactive program {
  K_ARROW_RIGHT -> {
    indice := nroBolitas(Rojo) + 1
    IrAlOrigen()
    LimpiarTablero()
    TestsDeCartasEspañolasDe40(indice)
    IrAlOrigen()
    Poner__Veces(Rojo, 1)
  }
  K_ARROW_LEFT -> {
    indice := nroBolitas(Rojo) - 1
    IrAlOrigen()
    LimpiarTablero()
    TestsDeCartasEspañolasDe40(indice)
    IrAlOrigen()
    Sacar__Veces(Rojo, 1)
  }
  K_CTRL_ARROW_RIGHT -> {
    indice := nroBolitas(Rojo) + 10
    IrAlOrigen()
    LimpiarTablero()
    TestsDeCartasEspañolasDe40(indice)
    IrAlOrigen()
    Poner__Veces(Rojo, 10)
  }
}
